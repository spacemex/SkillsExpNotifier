plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id("com.github.johnrengelman.shadow") version "8.1.1"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url = uri("https://maven.puffish.net") }
	maven { url = uri("https://jitpack.io") }
}

configurations {
	shaded.extendsFrom(implementation)
}

loom {
	splitEnvironmentSourceSets()
	mods {
		"skillexpnotifier" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "net.puffish:skillsmod:0.16.2+1.20:fabric"

	implementation "org.yaml:snakeyaml:2.4"
	implementation "com.github.spacemex:SimpleConfigApi:manual-5"
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

task createIntermediateJar(type: Jar) {
	archiveClassifier.set('intermediate')
	from sourceSets.main.output
	from sourceSets.client.output
}


// Create the shadowed JAR that contains all dependencies and our code
task shadedJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	archiveClassifier.set('shaded')

	from createIntermediateJar.outputs

	configurations = [project.configurations.runtimeClasspath]

	// Exclude what we don't need from the dependencies
	exclude "META-INF/LICENSE"
	exclude "META-INF/LICENSE.txt"
	exclude "META-INF/NOTICE"
	exclude "META-INF/NOTICE.txt"
	exclude "module-info.class"

	// Only include Snakeyaml and SimpleConfigApi
	dependencies {
		include(dependency("org.yaml:snakeyaml:2.4"))
		include(dependency("com.github.spacemex:SimpleConfigApi:manual-5"))
	}

	// Relocate libraries to avoid conflicts
	relocate 'org.yaml.snakeyaml', 'com.github.spacemex.shadowed.snakeyaml'
	relocate 'com.github.spacemex.simpleconfig', 'com.github.spacemex.shadowed.simpleconfig'

	// Merge service files correctly
	mergeServiceFiles()
}

// Configure remapJar to use the shadowed jar
remapJar {
	dependsOn shadedJar
	input.set(shadedJar.archiveFile)
	archiveClassifier.set('')  // No classifier for the final jar
}

jar {
	inputs.property "archivesName", project.base.archivesName
	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}" }
	}
	enabled = false
}

build.dependsOn remapJar


publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	repositories {
		// Optional: configure where to publish
	}
}
